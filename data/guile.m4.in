#
# Copyright (C) 2007, 2008 Francesco Salvestrini
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

AC_DEFUN([CT_PROG_GUILE],[
  AC_PATH_PROG(GUILE,guile)
  if test "x$GUILE" = "x"; then
	AC_MSG_ERROR(guile required but not found)
  fi
  AC_SUBST(GUILE)

  AC_PATH_PROG(GUILE_TOOLS,guile-tools)
  if test "x$GUILE_TOOLS" = "x"; then
	AC_MSG_ERROR(guile-tools required but not found)
  fi
  AC_SUBST(GUILE_TOOLS)

  AC_MSG_CHECKING([guile version])

  changequote(<<, >>)
  guile_version=`$GUILE_TOOLS --version 2>&1 | sed -e 's,.* \([0-9]*\.[0-9]*\.[0-9]*\),\1,'`
  guile_major_ver=`expr $guile_version : "\([0-9]\+\)\.[0-9]\+\.*[0-9]*"`
  guile_minor_ver=`expr $guile_version : "[0-9]\+\.\([0-9]\+\)\.*[0-9]*"`
  guile_micro_ver=`expr $guile_version : "[0-9]\+\.[0-9]\+\.*\([0-9]*\)" "|" 0`
  changequote([, ])

  AC_MSG_RESULT($guile_version)

  GUILE_VER=`printf "%02d%02d%02d" $guile_major_ver $guile_minor_ver $guile_micro_ver`
  GUILE_REQ=$1
  if test -n "$GUILE_REQ"; then
    AC_MSG_CHECKING(requested guile version >= $GUILE_REQ)
    changequote(<<, >>)
    guile_major_req=`expr $GUILE_REQ : '\([0-9]*\)\.[0-9]*\.[0-9]*'`
    guile_minor_req=`expr $GUILE_REQ : '[0-9]*\.\([0-9]*\)\.[0-9]*'`
    guile_micro_req=`expr $GUILE_REQ : '[0-9]*\.[0-9]*\.\([0-9]*\)'`
    changequote([, ])
    
    if test $guile_major_ver -ge $guile_major_req && test $guile_minor_ver -ge $guile_minor_req && test $guile_micro_ver -ge $guile_micro_req; then
      AC_MSG_RESULT(yes)
    else
      AC_MSG_RESULT(no)
      AC_MSG_ERROR(guile version >= $GUILE_REQ required)
    fi
  fi
])
