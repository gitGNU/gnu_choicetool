m4_dnl
m4_dnl  This file contains parts of Autoconf acgeneral.m4 file which have been
m4_dnl  adapted to choicetool
m4_dnl 
m4_dnl  Copyright (C) 1992, 93, 94, 95, 96, 1998 Free Software Foundation, Inc.
m4_dnl
m4_dnl This program is free software; you can redistribute it and/or modify
m4_dnl it under the terms of the GNU General Public License as published by
m4_dnl the Free Software Foundation; either version 2 of the License, or
m4_dnl (at your option) any later version.
m4_dnl
m4_dnl This program is distributed in the hope that it will be useful,
m4_dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
m4_dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
m4_dnl GNU General Public License for more details.
m4_dnl
m4_dnl You should have received a copy of the GNU General Public License along
m4_dnl with this program; if not, write to the Free Software Foundation, Inc.,
m4_dnl 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
m4_dnl

m4_dnl _CT_PATH_PROG(VARIABLE, PROG-TO-CHECK-FOR [, VALUE-IF-NOT-FOUND [, PATH]])
m4_define([_CT_PATH_PROG],[
m4_dnl Extract the first word of "$2", so it can be a program name with args.
set dummy $2; ct_word=[$]2
_CT_MSG_CHECKING([for $ct_word])
_CT_CACHE_VAL(ct_cv_path_$1,
[case "[$]$1" in
  /*)
  ct_cv_path_$1="[$]$1" # Let the user override the test with a path.
  ;;
  ?:/*)
  ct_cv_path_$1="[$]$1" # Let the user override the test with a dos path.
  ;;
  *)
  IFS="${IFS=   }"; ct_save_ifs="$IFS"; IFS=":"
m4_dnl $ct_dummy forces splitting on constant user-supplied paths.
m4_dnl POSIX.2 word splitting is done only on the output of word expansions,
m4_dnl not every word.  This closes a longstanding sh security hole.
  ct_dummy="m4_ifelse([$4], , $PATH, [$4])"
  for ct_dir in $ct_dummy; do
    test -z "$ct_dir" && ct_dir=.
    if test -f $ct_dir/$ct_word; then
      ct_cv_path_$1="$ct_dir/$ct_word"
      break
    fi
  done
  IFS="$ct_save_ifs"
m4_dnl If no 3rd arg is given, leave the cache variable unset,
m4_dnl so _CT_PATH_PROGS will keep looking.
m4_ifelse([$3], , , [  test -z "[$]ct_cv_path_$1" && ct_cv_path_$1="$3"
])
 ;;
esac])
$1="$ct_cv_path_$1"
if test -n "[$]$1"; then
  _CT_MSG_RESULT([$]$1)
else
  _CT_MSG_RESULT(no)
fi
])

m4_define([_CT_FD_MSG],[6])
if test "$silent" = yes; then
  exec _CT_FD_MSG>/dev/null
else
  exec _CT_FD_MSG>&1
fi

m4_define([_CT_MSG_RESULT],[
echo "$ct_t""$1" 1>&_CT_FD_MSG
m4_dnl echo "$2"
])

m4_define([_CT_MSG_CHECKING],[
echo $ct_n "checking $1""... $ct_c" 1>&_CT_FD_MSG
m4_dnl echo "checking $1..."
])

m4_dnl _CT_CACHE_VAL(CACHE-ID, COMMANDS-TO-SET-IT)
m4_define([_CT_CACHE_VAL],[
m4_dnl We used to use the below line, but it fails if the 1st arg is a
m4_dnl shell variable, so we need the eval.
m4_dnl if test "${$1+set}" = set; then
m4_dnl the '' avoids an AIX 4.1 sh bug ("invalid expansion").
if eval "test \"`echo '$''{'$1'+set}'`\" = set"; then
  echo $ct_n "(cached) $ct_c" 1>&_CT_FD_MSG
else
  $2
fi
])
