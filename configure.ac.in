# Process this file with autoconf to produce a configure script.

#
# configure.ac for choicetool
#
# Copyright (C) 2007, 2008 Francesco Salvestrini
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

AC_INIT([Choicetool],[@VERSION@],[salvestrini@gmail.com],[choicetool])

# Check for a recent version of autoconf
AC_PREREQ(2.61)

# Place/find all autotools related files in the following directories
AC_CONFIG_AUX_DIR([tools/autotools])
AC_CONFIG_MACRO_DIR([tools/autotools/m4])
AC_CONFIG_SRCDIR([src/choicetool.in])

#
# Build related programs
#
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S
AC_PROG_GREP
AC_PROG_SED
AX_PATH_MISSING([YAPP],     [yapp])
AX_PATH_MISSING([HELP2MAN], [help2man])
AX_PATH_MISSING([SITETOOL], [sitetool])

AC_PATH_PROG([WGET],        [wget])

#
# Runtime related programs
#
AX_WITH_PERL
AS_IF([test -z "$PERL"],[
	AC_MSG_ERROR([Missing required Perl interpreter])
],[
	AX_PROG_PERL_VERSION([5.8.0],[],[
		AC_MSG_ERROR([Missing required Perl interpreter version >= 5.8.0])
	])

	PERL_AUTOFRISK_CHECKS
	PERL_AUTOFRISK_SUMMARY
])

#
# A scheme interpreter (like Guile) is required ... fail if we cannot find it
#
AX_WITH_GUILE
AS_IF([test -z "$GUILE"],[
	AC_MSG_ERROR([Missing required Guile interpreter])
],[
	AX_PROG_GUILE_VERSION([1.6.4],[],[
		AC_MSG_ERROR([Missing required Guile interpreter version >= 1.6.4])
        ])

	#GUILE_MODULE_REQUIRED([sxml simple])
	#GUILE_MODULE_REQUIRED([sxml transform])

	GUILE_AUTOFRISK_CHECKS
	GUILE_AUTOFRISK_SUMMARY
])

#
# For automake options (AM_INIT_AUTOMAKE options) see Makefile.am 
# file, AUTOMAKE_OPTIONS variable
#
AM_INIT_AUTOMAKE([1.10 gnu -Wall])

#
# The API version is the base version.  We must guarantee
# compatibility for all releases with the same API version.
# Our current rule is that:
#
# * All releases, including the prereleases, in an X.Y series
#   are compatible.  So 1.5.1c is compatible with 1.5.
#
# * Prereleases on the trunk are all incompatible -- 1.5b and 1.5c
#   aren't the same.
#
API_VERSION=`echo "$PACKAGE_VERSION" | sed -e 's/^\([[0-9]]*\.[[0-9]]*[[a-z]]*\).*$/\1/'`
AC_SUBST(API_VERSION)

# Versioned directories, defined here for convenience
AC_SUBST(pkgvdir,     ["\${datadir}/${PACKAGE}-${API_VERSION}"])
AC_SUBST(pkgvbindir,  ["\${datadir}/${PACKAGE}-${API_VERSION}/bin"])
AC_SUBST(pkgvdatadir, ["\${datadir}/${PACKAGE}-${API_VERSION}/data"])
AC_SUBST(pkgvlibsdir, ["\${datadir}/${PACKAGE}-${API_VERSION}/libs"])

AC_CONFIG_FILES([
	Makefile

	data/Makefile

	tools/Makefile
	tools/scripts/Makefile
	tools/autotools/Makefile
	tools/autotools/m4/Makefile

	libs/Makefile
	libs/scheme/Makefile
	libs/perl/Makefile
	libs/perl/Choicetool/Makefile
	libs/perl/Choicetool/Base/Makefile
	libs/perl/Choicetool/OS/Makefile

	src/Makefile

	site/Makefile

	docs/Makefile
	docs/info/Makefile
	docs/man/Makefile

	tests/Makefile
])

AC_OUTPUT

# Header
cat <<EOF

  ${PACKAGE_NAME} ${API_VERSION} (${PACKAGE_VERSION}) configuration summary

EOF

# Body
if test -n "$PERL" ; then
   echo "    Perl     : ${PERL} (${PERL_VERSION})"
else
   echo "    Perl     : not available"
fi
if test -n "$GUILE" ; then
   echo "    Guile    : ${GUILE} (${GUILE_VERSION})"
else
   echo "    Guile    : not available"
fi

# Footer
cat <<EOF

EOF
